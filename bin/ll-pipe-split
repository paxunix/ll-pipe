#!/usr/bin/env perl

use strict;

use File::Basename;
use Getopt::Long;
use IO::File;
use IO::Handle;


sub usage()
{
    my $prog = basename($0);

    print(<<EOF);
NAME
    $prog
    
SYNOPSIS
    $prog [--lines-per-chunk num]
          [--directory dir]
          [file1 file2 ...]

DESCRIPTION
    Breaks stdin or the given files into files in the given director, each
    containing the given number of lines until all input is consumed.  Each
    create filename is output to stdout once content is written to it and it
    is closed.

OPTIONS
    --lines-per-chunk num
        The number of lines of input to write to each file "chunk".
        Defaults to 1000.

    --directory
        The directory to contain the split files.  Must already exist.
        Defaults to ".".

OPERANDS
    file1, file2, ...       Optional list of filenames used, in order, as
                            input.  If not given, input from stdin is
                            assumed.
EOF
}


my $verboseHandle = new_from_fd IO::Handle(3, "w");
sub verbose(@)
{
    if ($ENV{ll_pipe_verbose} && $verboseHandle)
    {
        $verboseHandle->print(@_);
    }
}


sub closefile($$)
{
    my ($fileHandle, $fileName) = @_;

    if ($fileHandle)
    {
        $fileHandle->close();
        print("$fileName\n");
    }
}


# Main

my $linesPerChunk = 1000;
my $directory = ".";

GetOptions(
    "lines-per-chunk=i", \$linesPerChunk,
    "directory=s", \$directory,
) or (usage(), exit(1));

if (! -d $directory)
{
    die("--directory '$directory' must already exist.\n");
}

my $fileHandle;
my $fileName;
my $lineCount = 0;
my $fileCount = 1;

# Force flush on stdout after printing name of each chunk.
$| = 1;

while (<>)
{
    if (!$fileHandle)
    {
        $fileName = "$directory/$fileCount";
        $fileHandle = new IO::File($fileName, ">") or
            die("Failed to open '$fileName' for write: $!\n");

        verbose("++ Writing to chunk $fileName\n");

        $fileCount++;
    }

    print($fileHandle $_);
    $lineCount++;

    if ($lineCount == $linesPerChunk)
    {
        closefile($fileHandle, $fileName);
        $fileHandle = undef;
        $lineCount = 0;
    }
}

closefile($fileHandle, $fileName);

verbose("++ Input split into " . ($fileCount - 1) . " chunks.\n");

my $chunkCountFile = "$directory/chunk-count";
open(COUNT, ">", $chunkCountFile) or
    die("Failed to open '$chunkCountFile' for write: $!\n");
print(COUNT ($fileCount - 1), "\n");
